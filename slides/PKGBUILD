#
# Maintainer: Mahmoud Mohamed (00xWolf) <mmsaeed509@gmail.com> , <https://github.com/mmsaeed509>
#

pkgname=slides
pkgver=0.9.0
pkgrel=1
pkgdesc='Terminal based presentation tool'
arch=('any')
url="https://github.com/maaslalani/$pkgname"
license=('MIT')
depends=('glibc')
makedepends=('go' 'git')
conflicts=("$pkgname-git" "$pkgname-bin")
source=("$pkgname::git+$url.git#tag=v$pkgver")
sha256sums=('SKIP')
changelog='CHANGELOG.md'
groups=('Exodia-TUI-Apps')

build() {

    cd $pkgname
    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export CGO_LDFLAGS="${LDFLAGS}"
    go clean -cache
    go clean -testcache
    go build -buildmode=pie -trimpath -ldflags "-linkmode external -X main.Version=${pkgver} -s -w"

}

package() {

    cd $pkgname
    install -Dm755 $pkgname "$pkgdir/usr/bin/$pkgname"
    install -Dm644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

}
